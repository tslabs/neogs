
;LAST UPDATE: 29.09.2014 savelij

;FAT   
Wc_fat		EQU 0				; FAT 
Getfzap		EQU 1				;  
Getlong		EQU 2				;   
Finddir		EQU 3				;  
Findfil		EQU 4				;    
Set_fil		EQU 5				;  
Nextsec		EQU 6				;   MP3 
Openfil		EQU 7				; 
Prvdir		EQU 8				; DIR  PLAY
Nxtdir		EQU 9				; DIR  PLAY
Loadfil		EQU 0X0A			; 

BUF_512		EQU B512BAS			;200  
TDIRCLS		EQU BUF_512+0X200		;400   ROOT 
DIR_CEP		EQU TDIRCLS+0X0400		;100   
ERORDRV		EQU DIR_CEP+0X0100		;2     A
CAL_FAT		EQU ERORDRV+2			;1  FAT
MANYFAT		EQU CAL_FAT+1			;1  FAT-
BYTSSEC		EQU MANYFAT+1			;1    
ROOTCLS		EQU BYTSSEC+1			;4   ROOT 
ROOTSEC		EQU ROOTCLS+4			;2    ROOT 
SEC_FAT		EQU ROOTSEC+2			;4    
RSVDSEC		EQU SEC_FAT+4			;2   
STARTRZ		EQU RSVDSEC+2			;4  /
FRSTDAT		EQU STARTRZ+4			;4      BPB
SEC_DSC		EQU FRSTDAT+4			;4    /
CLS_DSC		EQU SEC_DSC+4			;4    /
FATSTR		EQU CLS_DSC+4			;4   FAT
FILE_SZ		EQU FATSTR+4			;4    
KOLWCLS		EQU FILE_SZ+4			;4   
LST0ZAP		EQU KOLWCLS+4			;4   
LSTLOAD		EQU LST0ZAP+4			;4     
TEK_DIR		EQU LSTLOAD+4			;4   DIR
TEK_ZAP		EQU TEK_DIR+4			;2    DIR
TEK_DIRROOT	EQU TEK_ZAP+2			;2     ROOT
SAVEZAP		EQU TEK_DIRROOT+2		;2   
TEK_LVL		EQU SAVEZAP+2			;1   
TEK_LEV		EQU TEK_LVL+1			;1    
NUMSECK		EQU TEK_LEV+1			;1    
PAGEDIR		EQU NUMSECK+1			;2     
PAGEMP3		EQU PAGEDIR+2			;2     
TFILCLS		EQU PAGEMP3+2			;4  
TEKNUMC		EQU TFILCLS+4			;2   
MPHWOST		EQU TEKNUMC+2			;1     
KOL_CLS		EQU MPHWOST+1			;4 -   
KOLVMP3		EQU KOL_CLS+4			;2 -  
KOLVDIR		EQU KOLVMP3+2			;2 -  
STR_MP3		EQU KOLVDIR+2			;2    
STR_DIR		EQU (HIGH (STR_MP3)+1)*0X100	;?    DIR

;      FAT
		CALL CMP_INT
		DI
		EXX
		PUSH IX
		PUSH IY
		LD (ERORDRV),SP
		PUSH HL
		LD HL,EXITDRV
		EX (SP),HL
		PUSH HL
		PUSH DE
		ADD A,A
		LD L,A
		LD H,0
		LD DE,TABLFAT
		ADD HL,DE
		EX AF,AF'
		LD E,(HL)
		INC HL
		LD D,(HL)
		EX DE,HL
		POP DE
		EX (SP),HL
		EXX
		RET

ERR_DRV		LD SP,(ERORDRV)
EXITDRV		POP IY
		POP IX
		PUSH AF
		LD A,(INT_MODE)
		AND A
		JR Z,EXITDRV1
		EI
EXITDRV1	POP AF
		RET

TABLFAT		DW WC_FAT			;00  INIT FAT
		DW GETFZAP			;01 HL=FILE OPISATEL
		DW GETLONG			;02 GET LONGNAME
		DW FINDDIR			;03 FIND ALL DIR
		DW FINDMP3			;04 FIND ALL MP3
		DW SET_MP3			;05 SET NUMBER FILES
		DW NEXTSEC			;06 PLAY NEXT SECTOR
		DW OPENFIL			;07 OPEN FILE
		DW PRVDIR			;08 NEXT DIR 4 PLAY
		DW NXTDIR			;09 PREVIOUS DIR 4 PLAY
		DW LOADFIL			;0A LOAD FILE

NO_LNG		POP DE
		LD BC,8
		LDIR
		LD A,(HL)
		CP 0X20
		JR Z,NO_LNG1
		EX DE,HL
		LD (HL),0X2E
		INC HL
		EX DE,HL
		LDI
		LDI
		LDI
NO_LNG1		EX DE,HL
		LD (HL),0
		POP HL
		RET

GETLONG		PUSH HL
		PUSH HL
		CALL SET_MP3
		CALL GETFZAP
		BIT 0,E
		JR Z,NO_LNG
		EXX
		LD C,0
		EXX
GETLNG2		DEC BC
		CALL GETRZAP
		LD A,(HL)
		INC HL
		EXX
		BIT 6,C
		POP HL
		JR NZ,GETLNGE+1
		LD C,A
		EXX
		LD A,5
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		INC HL
		LD A,6
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		LD A,2
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		EXX
		PUSH HL
		EXX
		JR GETLNG2

GETLNGE		EXX
		LD (HL),0
		EXX
		POP HL
		RET

GETLNG1		EXX
		LD B,A
		EXX
		LD D,(HL)
		INC HL
		LD E,(HL)
		INC HL
		LD A,D
		OR E
		RET Z
		LD A,E
		AND A
		JR NZ,GETLNG3
		LD A,D
		CP 0X80
		JR C,GETLNG5
		LD D,0X5F
		JR GETLNG5

GETLNG3		CP 4
		LD A,0X5F
		JR NZ,GETLNG5
		LD A,D
		LD E,0XEF
		LD D,0X5F
		CP 1
		JR Z,GETLNG4
		LD E,0XA0
		CP 0X51
		JR Z,GETLNG4
		SUB 0X10
		LD E,0X80
		JR NC,GETLNG6
		LD A,D
		JR GETLNG5

GETLNG6		CP 0X30
		JR C,GETLNG4
		LD E,0XB0
		CP 0X40
		JR C,GETLNG4
		LD A,D
		JR GETLNG5

GETLNG4		ADD A,E
GETLNG5		EXX
		LD (HL),A
		INC HL
		DEC B
		EXX
		RET Z
		JR GETLNG1+3
;401,410-44F,451

LOADLST		CALL CPNUMSC
		JR NZ,LOADLST1
		LD HL,BUF_512
		RET
		
LOADLST1	LD (LSTLOAD),DE
		LD (LSTLOAD+2),BC
		LD HL,BUF_512
		PUSH HL
		EX AF,AF'
		LD A,2
		CALL COM__SD
		AND A
		JP NZ,ERR_DRV
		POP HL
		RET

;    
CPNUMSC		LD HL,LSTLOAD
		LD A,(HL)
		INC HL
		CP E
		RET NZ
		LD A,(HL)
		INC HL
		CP D
		RET NZ
		LD A,(HL)
		INC HL
		CP C
		RET NZ
		LD A,(HL)
		CP B
		RET

ENT_DIR		CALL GETFZAP
		BIT 4,A
		EX AF,AF'
		LD A,E
		EX AF,AF'
		LD E,A
		LD D,(HL)
		RET Z
		EXX
		EX AF,AF'
		BIT 7,A
		JR Z,ENT_DI2
		LD HL,TEK_LEV
		LD A,(HL)
		AND A
		EXX
		RET Z
		EXX
		DEC A
		LD (HL),A
		ADD A,A
		LD H,HIGH (DIR_CEP)
		LD L,A
		LD A,(HL)
		INC L
		LD H,(HL)
		LD L,A
		CALL ENT_DI1
		EXX
		LD (TEK_ZAP),HL
		EXX
		RET

ENT_DI2		LD HL,TEK_LEV
		LD A,(HL)
		INC A
		CP 0X80
		EXX
		RET Z
		EXX
		LD (HL),A
		DEC A
		ADD A,A
		LD H,HIGH (DIR_CEP)
		LD L,A
		LD BC,(TEK_ZAP)
		LD (HL),C
		INC L
		LD (HL),B
ENT_DI1		EXX
		LD DE,20
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		LD A,B
		OR C
		OR D
		OR E
		JR NZ,ENT_DI0
		LD DE,(ROOTCLS)
		LD BC,(ROOTCLS+2)
ENT_DI0		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
		CALL INIRTSC
		XOR A
		INC A
		RET

GETFZAP		LD BC,(TEK_ZAP)
		LD A,B
		OR C
		JR Z,GETRZAP
		PUSH BC
		DEC BC
		CALL GETRZAP
		POP BC
		LD E,A
GETRZAP		PUSH DE
		CALL RDDIRSC
		POP DE
		CP 0XFF
		LD A,E
		RET Z
		EX AF,AF'
GETFZA1		LD A,C
		AND 0X0F
		LD E,A
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		LD DE,11
		EX DE,HL
		ADD HL,DE
		LD L,(HL)
		LD H,0
		EX DE,HL
		CALL CP_TWOT
		JR NZ,$+4
		LD D,0X80
		EX AF,AF'
		CP 0X0F
		LD A,E
		LD E,D
		RET NZ
		SET 0,E
		RET

;  DIR   BC
; : A=0XFF-   DIR
RDDIRSC		PUSH BC
		LD D,B
		LD E,C
		LD BC,0
		LD A,0X10
		CALL BCDE_A
		LD A,E
		PUSH AF
		LD A,(BYTSSEC)
		PUSH AF
		CALL BCDE_A
		LD HL,TDIRCLS
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		CALL LOADZP
		CALL REALSEC
		POP AF
		DEC A
		LD L,A
		POP AF
		AND L
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		CALL LOADLST
		POP BC
		RET

LOADZP		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		INC HL
		RET

SAVEZP		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		LD (HL),C
		INC HL
		LD (HL),B
		INC HL
		RET

INIRTSC		PUSH AF
		LD HL,TDIRCLS			;    
		LD A,D
		OR E
		OR B
		OR C
		CALL SAVEZP			;     
		JR Z,LASTCLS			;   0,   ROOT  ( 12/16)
NEXTCLS		PUSH HL
		CALL RDFATZP			;      
		CALL LST_CLS			;   
		POP HL
		JR C,LASTCLS
		CALL SAVEZP			;     
		JR NEXTCLS			;  

LASTCLS		LD BC,0XFFFF
		CALL SAVEZP			;   
		INC BC
		CALL RDDIRSC
		LD A,(HL)
		CP "."
		JR NZ,INIRTS1
		INC HL
		LD A,(HL)
		CP "."
		JR Z,INIRTS1
		INC BC
INIRTS1		LD (TEK_ZAP),BC
		POP AF
		RET

CP_TWOT		LD A,(HL)
		CP "."
		RET NZ
		INC HL
		LD A,(HL)
		DEC HL
		CP "."
		RET NZ
		XOR A
		RET

LST_CLS		LD A,(CAL_FAT)
		AND A
		JR NZ,LST_CL1
		LD HL,0X0FF7
		SBC HL,DE
		RET

LST_CL1		DEC A
		JR NZ,LST_CL2
		LD HL,0XFFF7
		SBC HL,DE
		RET

LST_CL2		PUSH BC
		LD A,B
		AND 0X0F
		LD B,A
		LD HL,0X0FFF
		SBC HL,BC
		POP BC
		RET NZ
		LD HL,0XFFF7
		SBC HL,DE
		RET

RDFATZP		LD A,(CAL_FAT)			;    
		AND A
		JR Z,RDFATS0			;   12
		DEC A
		JR Z,RDFATS1			;   16
		EX DE,HL			;   32
		ADD HL,HL
		EX DE,HL
		LD HL,0
		ADC HL,BC
		ADC HL,BC			;    2
		LD A,E
		LD E,D
		LD D,L
		LD C,H
		LD B,0				;    256
		CALL RDFATS2			;  16     16
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)			;   16 
		RET

; 16       16
RDFATS1		LD BC,0
		LD A,E
		LD E,D
		LD D,C				;    256,  16  =0
RDFATS2		PUSH AF				;  16     16/32
		PUSH BC
		LD HL,FATSTR
		CALL BCDEHLP			;     
		CALL LOADLST			;   
		POP BC
		POP AF
		LD E,A
		LD D,0
		ADD HL,DE
		ADD HL,DE			;       
		LD E,(HL)
		INC HL
		LD D,(HL)			; 16   
		RET

; 12       12
RDFATS0		LD H,D
		LD L,E
		ADD HL,HL
		ADD HL,DE			;HL=HL*3
		SRL H
		RR L				;HL=HL/2 -       1,5
		LD A,E				;A-       
		LD E,H
		LD D,0
		LD B,D
		LD C,D				;    256
		SRL E
		PUSH AF
		PUSH HL
		LD HL,FATSTR
		CALL BCDEHLP			;     
		CALL LOADLST			;  
		POP BC
		LD A,B
		AND 1
		LD B,A				;BC=   
		ADD HL,BC			;HL=     
		LD B,(HL)			;    
		INC HL				;  
		LD A,H
		CP HIGH (BUF_512)+2		;     
		JR NZ,RDFATS4
		PUSH BC				;     
		LD BC,0
		INC DE
		CALL LOADLST			;    
		POP BC
RDFATS4		POP AF
		LD D,(HL)			;    
		LD E,B				; DE=    
		LD BC,0
		RRA				;  0   
		JR NC,RDFATS3
		SRL D				;       12 
		RR E
		SRL D
		RR E
		SRL D
		RR E
		SRL D
		RR E
RDFATS3		LD A,D
		AND 0X0F
		LD D,A				;   4     
		RET

;  
;  BCDE= FAT
;  BCDE= 
REALSEC		LD A,B
		OR C
		OR D
		OR E
		JR NZ,REALSE1			;BCDE=0?
		LD HL,SEC_FAT			; ROOT   12/16
		LD DE,(FATSTR)			; ROOT     
		LD BC,(FATSTR+2)
		PUSH HL
		CALL BCDEHLP			;      
		POP HL
		JP BCDEHLP			;        ROOT 

REALSE1		LD HL,0XFFFE
		EX DE,HL
		ADD HL,DE
		EX DE,HL
		INC HL
		ADC HL,BC			;HLDE= -2
		LD A,(BYTSSEC)			;    
		JR REALSE2

REALSE3		SLA E
		RL D
		RL L
		RL H
REALSE2		RRCA
		JR NC,REALSE3			;   
		LD B,H
		LD C,L
		LD HL,STARTRZ
		CALL BCDEHLP			;    
		LD HL,FRSTDAT
		JP BCDEHLP			;    

BCDE200		LD E,D
		LD D,C
		LD C,B
		LD B,0
		LD A,2
		JR BCDE_A

;BCDE>>A=BCDE
BCDE_A1		SRL B
		RR C
		RR D
		RR E
BCDE_A		RRCA
		JR NC,BCDE_A1
		RET

;(ADR)-BCDE=BCDE
BCDEHLM		LD A,(HL)
		INC HL
		SUB E
		LD E,A
		LD A,(HL)
		INC HL
		SBC A,D
		LD D,A
		LD A,(HL)
		INC HL
		SBC A,C
		LD C,A
		LD A,(HL)
		SBC A,B
		LD B,A
		RET

;(ADR)+BCDE=BCDE
BCDEHLP		LD A,(HL)
		INC HL
		ADD A,E
		LD E,A
		LD A,(HL)
		INC HL
		ADC A,D
		LD D,A
		LD A,(HL)
		INC HL
		ADC A,C
		LD C,A
		LD A,(HL)
		ADC A,B
		LD B,A
		RET

;HLDE+BC=HLDE
HLDEPBC		EX DE,HL
		ADD HL,BC
		EX DE,HL
		LD BC,0
		ADC HL,BC
		RET

;BCDE-(ADR)=BCDE
HLBCDEM		LD A,E
		SUB (HL)
		INC HL
		LD E,A
		LD A,D
		SBC A,(HL)
		INC HL
		LD D,A
		LD A,C
		SBC A,(HL)
		INC HL
		LD C,A
		LD A,B
		SBC A,(HL)
		LD B,A
		RET

FAT_ERR		LD A,0XFF
		LD (CAL_FAT),A
		RET

;  FAT
WC_FAT		LD A,0XFF
		LD (LSTLOAD+3),A
		LD DE,0
		LD B,D
		LD C,E
		CALL LOADLST
		PUSH HL
		POP IY
		LD DE,0X01BE
		ADD HL,DE
		LD A,(HL)
		AND A
		JR NZ,RDFAT05
		LD DE,4
		ADD HL,DE
		LD A,(HL)
		LD B,0
		CP 1
		JR Z,RDFAT06
		LD B,2
		CP 0X0B
		JR Z,RDFAT06
		CP 0X0C
		JR Z,RDFAT06
		LD B,1
		CP 6
		JR Z,RDFAT06
		CP 0X0E
		JR NZ,RDFAT05
RDFAT06		LD A,B
		LD (CAL_FAT),A
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		JR RDFAT00

RDFAT05		LD C,(IY+0X0D)
		XOR A
		LD E,A
		LD B,8
		RR C
		ADC A,0
		DJNZ $-4
		DEC A
		JR NZ,$+3
		INC E
		LD A,(IY+0X0E)
		OR (IY+0X0F)
		JR Z,$+3
		INC E
		LD A,(IY+0X13)
		OR (IY+0X14)
		JR NZ,$+3
		INC E
		LD A,(IY+0X20)
		OR (IY+0X21)
		OR (IY+0X22)
		OR (IY+0X23)
		JR NZ,$+3
		INC E
		LD A,(IY+0X15)
		AND 0XF0
		CP 0XF0
		JR NZ,$+3
		INC E
		LD A,E
		CP 4
		JP NZ,FAT_ERR
		LD A,0XFF
		LD (CAL_FAT),A
		LD DE,0
		LD B,D
		LD C,E

RDFAT00		LD (STARTRZ),DE
		LD (STARTRZ+2),BC		;    
		CALL LOADLST			; 
		LD HL,0
		LD DE,(BUF_512+0X16)		;BPB_FATSZ16
		LD A,D
		OR E
		JR NZ,RDFAT01			;  FAT12/16 (BPB_FATSZ16=0)
		LD DE,(BUF_512+0X24)
		LD HL,(BUF_512+0X26)		;BPB_FATSZ32
						;    +36
RDFAT01		LD (SEC_FAT+2),HL
		LD (SEC_FAT),DE			;   FAT-
		LD HL,0
		LD DE,(BUF_512+0X13)		;BPB_TOTSEC16
		LD A,D
		OR E
		JR NZ,RDFAT02			;  FAT12/16 (BPB_TOTSEC16=0)
		LD DE,(BUF_512+0X20)
		LD HL,(BUF_512+0X22)		;BPB_TOTSEC32
						;    +32
RDFAT02		LD (SEC_DSC+2),HL
		LD (SEC_DSC),DE			;-   /

; ROOTDIRSECTORS
		LD BC,(BUF_512+0X0B)		;BPB_BYTSPERSEC
		LD DE,(BUF_512+0X11)		;BPB_ROOTENTCNT
		LD HL,0
		LD A,D
		OR E
		JR Z,RDFAT03
		LD B,H
		LD C,L
		LD A,0X10
		CALL BCDE_A
		EX DE,HL

;  
;ROOTDIRSECTORS=((BPB_ROOTENTCNT*32)+(BPB_BYTSPERSEC-1))/BPB_BYTSPERSEC
; HL=ROOTDIRSECTORS.  FAT32,  HL=0 

RDFAT03		PUSH HL				;ROOTDIRSECTORS
		LD (ROOTSEC),HL
		LD A,(BUF_512+0X10)
		LD DE,(SEC_FAT)
		LD HL,(SEC_FAT+2)
		DEC A
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		DEC A
		JR NZ,$-6
		POP BC				;  FAT-  
		CALL HLDEPBC			; ROOTDIRSECTORS
		LD BC,(BUF_512+0X0E)		;BPB_RSVDSECCNT
		LD (RSVDSEC),BC
		CALL HLDEPBC			; BPB_RESVDSECCNT
		LD (FRSTDAT),DE
		LD (FRSTDAT+2),HL		;    
		LD B,H
		LD C,L
		LD HL,SEC_DSC			;BCDE+32-    HL
		CALL BCDEHLM			;   -  
		LD A,(BUF_512+0X0D)
		LD (BYTSSEC),A
		CALL BCDE_A			;  -   
		LD (CLS_DSC),DE
		LD (CLS_DSC+2),BC		; -   

		LD A,(CAL_FAT)
		CP 0XFF
		JR NZ,RDFAT04
;  FAT   MBR
		LD DE,(SEC_FAT-1)
		LD BC,(SEC_FAT+1)
		LD E,0				;BCDE=  *0X100
		PUSH BC
		PUSH DE				;
		SRL B
		RR C
		RR D
		RR E				;BCDE=  *0X80
		LD HL,CLS_DSC			;   FAT
		PUSH HL				;
		CALL HLBCDEM			; -( *0X80)
		LD A,E
		AND 0X80			;  128     FAT32
		OR D
		OR C
		OR B
		LD A,2
		POP HL
		POP DE
		POP BC
		JR Z,RDFAT04			;FAT32   Z=0
		CALL HLBCDEM			; -( *0X100)
		LD A,D
		OR C
		OR B
		LD A,1
		JR Z,RDFAT04			;FAT16   Z=0
		XOR A				; FAT12

; FAT12/16   
; 
; FAT32    +44
;  BCDE- ROOTDIR
RDFAT04		LD (CAL_FAT),A			;  
		EX AF,AF'
		LD DE,(RSVDSEC)
		LD BC,0
		LD HL,STARTRZ
		CALL BCDEHLP
		LD (FATSTR),DE
		LD (FATSTR+2),BC		;      FAT-T
		EX AF,AF'
		AND A
		LD DE,0
		LD B,D
		LD C,E
		JR Z,FSRROO2			;FAT12-NONE
		DEC A
		JR Z,FSRROO2			;FAT16
		LD DE,(BUF_512+0X2C)
		LD BC,(BUF_512+0X2E)		;FAT32
FSRROO2		LD (ROOTCLS),DE
		LD (ROOTCLS+2),BC		;   ROOT 
		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
FSRR121		XOR A
		LD (TEK_LEV),A
		LD A,(CAL_FAT)
		JP INIRTSC

F_EXT		DB "MP3MOD",0

CP_EXT		PUSH BC
		PUSH DE
		PUSH HL
		LD C,0
		LD DE,F_EXT
CPETR2		LD A,(DE)
		AND A
		JR Z,CPETR1+1
		INC C
		PUSH DE
		PUSH HL
		CALL COMPARF
		POP HL
		POP DE
		JR Z,CPETR1
		INC DE
		INC DE
		INC DE
		JR CPETR2

CPETR1		LD A,C
		POP HL
		POP DE
		POP BC
		RET

COMPARF		PUSH DE
		LD DE,8
		ADD HL,DE
		POP DE
		LD B,3
		LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		DJNZ $-5
		RET

OPENFIL		CALL SET_MP3
		CALL GETFZAP
		CALL CP_EXT
		PUSH AF
		LD DE,0X14
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		LD (TFILCLS),DE
		LD (TFILCLS+2),BC
		INC HL
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD A,D
		AND 1
		OR E
		LD (FILE_SZ),DE
		LD (FILE_SZ+2),BC
		EX AF,AF'
		CALL BCDE200
		LD A,(BYTSSEC)
		LD H,A
		DEC A
		AND E
		LD L,A
		EX AF,AF'
		JR Z,$+3
		INC L
		LD A,L
		LD (MPHWOST),A
		LD A,H
		CALL BCDE_A
		LD (KOL_CLS),DE
		LD (KOL_CLS+2),BC
		XOR A
		LD (NUMSECK),A
		POP AF
		RET

;    
; MP3
NEXTSEC		LD HL,KOL_CLS
		LD A,(HL)
		INC HL
		OR (HL)
		INC HL
		OR (HL)
		INC HL
		OR (HL)
		JR Z,LSTCLSF
		LD DE,(TFILCLS)
		LD BC,(TFILCLS+2)
		PUSH DE
		PUSH BC
		CALL REALSEC
		LD HL,(NUMSECK)
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		LD A,9
		CALL COM__SD
		AND A
		JP NZ,ERR_DRV
		POP BC
		POP DE
		LD A,(NUMSECK)
		INC A
		LD H,A
		LD (NUMSECK),A
		LD A,(BYTSSEC)
		CP H
		LD A,0				;   
		RET NZ
		XOR A
		LD (NUMSECK),A
		CALL RDFATZP
		LD (TFILCLS),DE
		LD (TFILCLS+2),BC
		LD HL,KOL_CLS
		LD DE,1
		LD B,D
		LD C,D
		CALL BCDEHLM
		LD (KOL_CLS),DE
		LD (KOL_CLS+2),BC
		XOR A
		INC A
		RET

;   
;  MP3
LSTCLSF		LD DE,(TFILCLS)
		LD BC,(TFILCLS+2)
		CALL REALSEC
		LD HL,(NUMSECK)
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		LD A,9
		CALL COM__SD
		AND A
		JP NZ,ERR_DRV
		LD A,(NUMSECK)
		INC A
		LD H,A
		LD (NUMSECK),A
		LD A,(MPHWOST)
		AND A
		RET Z
		CP H
		RET

;  
; 
SET_MP3		LD H,B
		LD L,C
		LD DE,(STR_MP3)
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		PUSH HL
		EX DE,HL
		LD DE,TEK_DIR
		LDI
		LDI
		LDI
		LDI
		LD DE,(TEK_DIR)
		LD BC,(TEK_DIR+2)
		CALL INIRTSC
		POP HL
		LD E,(HL)
		INC HL
		LD D,(HL)
		LD (TEK_ZAP),DE
		RET

;   
PRVDIR		LD HL,(KOLVDIR)
		LD DE,2
		AND A
		SBC HL,DE
		RET C
		LD HL,(KOLVMP3)
		AND A
		SBC HL,DE
		RET C
		LD H,B
		LD L,C
		LD DE,(STR_MP3)
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		PUSH HL
		POP IX
		LD H,B
		LD L,C
		LD BC,(KOLVMP3)
		LD DE,0XFFFC
		EXX
		LD L,(IX+0)
		LD H,(IX+1)
		EXX
		CALL PRVDIR2
		CALL PRVDIR2
		LD DE,4
		JR NXTDIR2

PRVDIR2		EXX
		LD D,H
		LD E,L
		LD L,(IX+0)
		LD H,(IX+1)
		AND A
		SBC HL,DE
		EX AF,AF'
		ADD HL,DE
		EX AF,AF'
		EXX
		RET NZ
		LD A,H
		OR L
		JR Z,PRVDIR4
		DEC HL
		ADD IX,DE
		JR PRVDIR2

PRVDIR4		LD H,B
		LD L,C
		DEC HL
		ADD HL,HL
		ADD HL,HL
		LD IX,(STR_MP3)
		EX DE,HL
		ADD IX,DE
		EX DE,HL
		LD H,B
		LD L,C
		DEC HL
		JR PRVDIR2

;   
NXTDIR		LD HL,(KOLVDIR)
		LD DE,2
		AND A
		SBC HL,DE
		RET C
		LD HL,(KOLVMP3)
		AND A
		SBC HL,DE
		RET C
		LD H,B
		LD L,C
		LD DE,(STR_MP3)
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		PUSH HL
		POP IX
		LD H,B
		LD L,C
		LD BC,(KOLVMP3)
		LD DE,4
		EXX
		LD L,(IX+0)
		LD H,(IX+1)
		EXX
NXTDIR2		EXX
		LD D,H
		LD E,L
		LD L,(IX+0)
		LD H,(IX+1)
		AND A
		SBC HL,DE
		EX AF,AF'
		ADD HL,DE
		EX AF,AF'
		EXX
		JR NZ,NXTDIR3
		INC HL
		ADD IX,DE
		AND A
		SBC HL,BC
		EX AF,AF'
		ADD HL,BC
		EX AF,AF'
		JR NZ,NXTDIR2
		LD HL,0
		LD IX,(STR_MP3)
		JR NXTDIR2

NXTDIR3		LD B,H
		LD C,L
		RET

;   
SETPAGE		LD HL,BUF_PAG+0XFF
		LD L,(HL)
		LD A,(HL)
		OUT (MPAGEX),A
		LD L,0XFF
		INC (HL)
		RET

; MOD   NEOGS
LOADFIL		LD HL,BUF_PAG+0XFF
		LD (HL),0
		IN A,(GSCFG0)
		SET B_EXPAG,A
		OUT (GSCFG0),A
		LD IY,(BYTSSEC)
;LY=-   
		LD A,IYL
		CP 0X20
		JR C,LDMF5
;  32   
LD_F5		LD DE,(TFILCLS)
		LD BC,(TFILCLS+2)
		CALL REALSEC
		LD A,IYL
		AND 0XE0
		RLCA
		RLCA
		RLCA
		LD IYH,A
LD_F9		LD A,0X20
		EX AF,AF'
		CALL SETPAGE
		LD A,3
		LD HL,0XC000
		CALL COM__SD
		AND A
		JP NZ,ERR_DRV
		LD HL,0X20
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		DEC IYH
		JR NZ,LD_F9
		LD DE,(TFILCLS)
		LD BC,(TFILCLS+2)
		CALL RDFATZP
		CALL LST_CLS
		JR C,LD_E
		LD (TFILCLS+2),BC
		LD (TFILCLS),DE
		JR LD_F5

LD_E		IN A,(GSCFG0)
		RES B_EXPAG,A
		OUT (GSCFG0),A
		LD A,(PG_PLAYER)
		OUT (MPAG),A
		LD A,(BUF_PAG+0XFF)
		SRL A
		ADC A,0
		RET

;  16   
LDMF5		CALL SETPAGE
		LD HL,0XC000
LDMF2		LD DE,(TFILCLS)
		LD BC,(TFILCLS+2)
		PUSH HL
		CALL REALSEC
		POP HL
LDMF3		LD A,IYL
		EX AF,AF'
		LD A,3
		CALL COM__SD
		AND A
		JP NZ,ERR_DRV
		PUSH HL
		LD BC,(TFILCLS+2)
		LD DE,(TFILCLS)
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR C,LD_E
		LD (TFILCLS+2),BC
		LD (TFILCLS),DE
		LD A,H
		AND A
		JR NZ,LDMF2
		JR LDMF5

;    FAT
FINDDIR		LD IY,0
		XOR A
		LD (TEK_LVL),A
		LD DE,STR_DIR
		LD HL,ROOTCLS
		LDI
		LDI
		LDI
		LDI
		LD (PAGEDIR),DE
		EXX
		LD A,(TEK_LVL)			;  
		AND A
		LD HL,0				;  ROOT 
		JR NZ,FNDDIR71
		LD A,(CAL_FAT)
		CP 2
		JR NC,FNDDIR71
		LD HL,(ROOTSEC)			;  ROOT 
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC HL
FNDDIR71	LD (TEK_DIRROOT),HL
		EXX
FNDDIR7		LD BC,0
		CALL RDDIRSC
		LD DE,0X20
		PUSH HL
		POP IX
		LD A,(HL)
		CP "."
		JR NZ,FNDDIR3
FNDDIR4		LD A,(IX+1)
		CP "."
		JR Z,FNDDIR1
		INC BC
		ADD IX,DE
FNDDIR1		INC BC
		ADD IX,DE
FNDDIR3		LD A,IXH
		CP HIGH (BUF_512)+2
		JR NZ,FNDDIR2
		LD A,(TEK_LVL)
		AND A
		JR NZ,FNDDIR311
		EXX
		LD HL,(TEK_DIRROOT)
		LD A,H
		OR L
		EXX
		JR Z,FNDDIR5
FNDDIR311	CALL RDDIRSC
		JR NZ,FNDDIR5			; DIR
		LD DE,0X20
		PUSH HL
		POP IX
FNDDIR2		LD A,(TEK_LVL)
		AND A
		JR NZ,FNDDIR21
		EXX
		LD HL,(TEK_DIRROOT)
		DEC HL
		LD (TEK_DIRROOT),HL
		LD A,H
		OR L
		EXX
		JR Z,FNDDIR5
FNDDIR21	LD A,(IX+0X0B)
		CP 0X0F
		JR Z,FNDDIR1
		CP 8
		JR Z,FNDDIR1
		LD A,(IX+0)
		CP 0XE5
		JR Z,FNDDIR1
		AND A
		JR Z,FNDDIR5			; DIR
		BIT 4,(IX+0X0B)
		JR Z,FNDDIR1
;  DIR
FNDDIR6		LD HL,(PAGEDIR)			; 
		LD A,(IX+0X1A)
		LD (HL),A
		INC HL
		LD A,(IX+0X1B)
		LD (HL),A
		INC HL
		LD A,(IX+0X14)
		LD (HL),A
		INC HL
		LD A,(IX+0X15)
		LD (HL),A
		INC HL				;    DIR
		LD (PAGEDIR),HL
		DEC IY
		DEC IY
		LD (IY),C
		LD (IY+1),B
		LD (TEK_ZAP),BC
		CALL ENT_DIR
		EXX
		LD HL,TEK_LVL
		INC (HL)
		EXX
		JP FNDDIR7

FNDDIR5		LD A,IYH
		AND A
		JR Z,FNDDIR0
		LD BC,0
		CALL RDDIRSC
		PUSH HL
		POP IX
		LD DE,0X20
		LD A,(HL)
		CP "."
		JR NZ,FNDDI52
		INC HL
		LD A,(HL)
		CP "."
		JR Z,FNDDI52
		INC BC
		ADD IX,DE
FNDDI52		LD (TEK_ZAP),BC
		CALL ENT_DIR
		LD C,(IY+0)
		LD B,(IY+1)
		INC IY
		INC IY
		CALL RDDIRSC
		LD A,C
		AND 0X0F
		LD E,A
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		PUSH HL
		POP IX
		LD DE,0X20
		EXX
		LD HL,TEK_LVL
		DEC (HL)
		EXX
		JP FNDDIR1

; DIR 
FNDDIR0		LD HL,(PAGEDIR)
		LD D,H
		LD E,L
		INC HL
		INC HL
		INC HL
		LD (HL),0XFF
		PUSH HL
		EX DE,HL
		LD DE,STR_DIR
		AND A
		SBC HL,DE
		SRL H
		RR L
		SRL H
		RR L
		LD (KOLVDIR),HL
		EX (SP),HL
		INC HL
		LD (STR_MP3),HL
		POP HL
		RET

;     
FNDMP30		LD HL,(PAGEMP3)
		LD DE,(STR_MP3)
		PUSH HL
		POP IX
		LD (HL),0
		INC L
		LD (HL),0
		DEC L
		AND A
		SBC HL,DE
		SRL H
		RR L
		SRL H
		RR L
		LD DE,2
		LD (KOLVMP3),HL
		LD (KOLWMP3),HL
		AND A
		SBC HL,DE
		EX AF,AF'
		ADD HL,DE
		EX AF,AF'
		RET C
		PUSH HL
		LD IY,1
		LD B,H
		LD C,L
		LD DE,0XFFFC
		ADD IX,DE
		DEC HL
		EXX
		LD L,(IX+0)
		LD H,(IX+1)
		EXX
FNDM030		EXX
		LD D,H
		LD E,L
		LD L,(IX+0)
		LD H,(IX+1)
		AND A
		SBC HL,DE
		EX AF,AF'
		ADD HL,DE
		EX AF,AF'
		EXX
		JR Z,$+4
		INC IY
		LD A,H
		OR L
		JR Z,FNDM033			; ,  
		DEC HL
		ADD IX,DE
		JR FNDM030

FNDM033		LD (KOLVDIR),IY			;  
		POP HL
		RET

;   
FINDMP3		LD HL,(STR_MP3)
		LD (PAGEMP3),HL
		LD HL,STR_DIR
		LD (PAGEDIR),HL
		XOR A
		LD (TEK_LVL),A
		EXX
		LD A,(TEK_LVL)			;  
		AND A
		LD HL,0				;  ROOT 
		JR NZ,FNDMP371
		LD A,(CAL_FAT)
		CP 2
		JR NC,FNDMP371
		LD HL,(ROOTSEC)			;  ROOT 
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC HL
FNDMP371	LD (TEK_DIRROOT),HL
		EXX
FNDMP37		LD HL,(PAGEDIR)
		LD (TEKNUMC),HL
		LD DE,TEK_DIR
		LDI
		LDI
		LDI
		LDI
		LD (PAGEDIR),HL
		DEC HL
		BIT 7,(HL)
		JP NZ,FNDMP30
		LD DE,(TEK_DIR)
		LD BC,(TEK_DIR+2)
		CALL INIRTSC
		LD BC,0
		CALL RDDIRSC
		LD DE,0X20
		PUSH HL
		POP IX
		LD A,(HL)
		CP "."
		JR NZ,FNDMP33
FNDMP34		LD A,(IX+1)
		CP "."
		JR Z,FNDMP31
		INC BC
		ADD IX,DE
FNDMP31		INC BC
		ADD IX,DE
FNDMP33		LD A,IXH
		CP HIGH (BUF_512)+2
		JR NZ,FNDMP32
		LD A,(TEK_LVL)
		AND A
		JR NZ,FNDMP331
		EXX
		LD HL,(TEK_DIRROOT)
		LD A,H
		OR L
		EXX
		JR Z,FNDMP37
FNDMP331	CALL RDDIRSC
		JP NZ,FNDMP37
		LD DE,0X20
		PUSH HL
		POP IX
FNDMP32		LD A,(TEK_LVL)
		AND A
		JR NZ,FNDMP321
		EXX
		LD HL,(TEK_DIRROOT)
		DEC HL
		LD (TEK_DIRROOT),HL
		LD A,H
		OR L
		EXX
		JR Z,FNDMP37
FNDMP321	LD A,(IX+0X0B)
		CP 0X0F
		JR Z,FNDMP31
		CP 8
		JR Z,FNDMP31
		LD A,(IX+0)
		CP 0XE5
		JR Z,FNDMP31
		AND A
		JP Z,FNDMP37
		BIT 4,(IX+0X0B)
		JR NZ,FNDMP31
		EXX
		PUSH IX
		POP HL
		CALL CP_EXT			;  
		EXX
		AND A
		JP Z,FNDMP31			; , 
		LD DE,(TEKNUMC)
		LD HL,(PAGEMP3)
;  
;2    
;2  ""  
		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		LD (HL),C
		INC HL
		LD (HL),B
		INC HL
		LD (PAGEMP3),HL
		LD A,H
		AND A
		JP Z,FNDMP30			; , 
		LD DE,0X20
		JR FNDMP31
